#==================================================
#| #Sounds.sk
#| 音源関係の実装・管理モジュール
#|
#| Nekozouneko Group Developer
#==================================================

using script reflection

function playSound(p: objects, soundId: string):
    run function named "soundHandle_%{_soundId}%" with arguments {_p::*}

local function soundHandle_Error(p: objects):
    play sound "minecraft:block.note_block.bass" with pitch 1 to {_p::*}

local function soundHandle_Click(p: objects):
    play sound "minecraft:ui.button.click" with pitch 1 to {_p::*}

local function soundHandle_OreReviveStart(p: objects):
    play sound "minecraft:block.note_block.bell" with pitch 1 to {_p::*}
    wait 2 tick
    play sound "minecraft:block.note_block.bell" with pitch 1.25 to {_p::*}

local function soundHandle_OreRevived(p: objects):
    play sound "minecraft:block.note_block.bell" with pitch 2 to {_p::*}

local function soundHandle_QuestOrdered(p: objects):
    play sound "minecraft:block.note_block.xylophone" with pitch 1.5 to {_p::*}

local function soundHandle_QuestComplete(p: objects):
    play sound "entity.player.levelup" with pitch 1.5 to {_p::*}

local function soundHandle_QuestRewards(p: objects):
    play sound "minecraft:entity.experience_orb.pickup" with pitch 1.5 to {_p::*}

local function soundHandle_Talk(p: objects):
    play sound "minecraft:entity.villager.ambient" with pitch 1 to {_p::*}

local function soundHandle_Levelup(p: objects):
    play sound "minecraft:entity.player.levelup" with pitch 1 to {_p::*}

local function soundHandle_ShopSold(p: objects):
    play sound "minecraft:block.note_block.harp" with pitch 0.7 to {_p::*}
    play sound "minecraft:block.note_block.harp" with pitch 0.9 to {_p::*}
    wait 2 tick
    play sound "minecraft:block.note_block.harp" with pitch 0.9 to {_p::*}
    play sound "minecraft:block.note_block.harp" with pitch 1.05 to {_p::*}
    wait 2 tick
    play sound "minecraft:block.note_block.harp" with pitch 0.94 to {_p::*}
    play sound "minecraft:block.note_block.harp" with pitch 1.18 to {_p::*}
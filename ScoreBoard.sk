#==================================================
#| #ScoreBoard.sk
#| スコアボードの実装・管理モジュール
#|
#| Nekozouneko Group Developer
#==================================================

function scoreboardUpdate(player: player):
    #Define Arguments
    set {_region} to getDefine("REGION_NAME::%getRegion({_player})%")
    set {_region} to "不明" if {_region} is not set
    add {_region} to {_args::*}
    add String(profileLevelGet({_player})) to {_args::*}
    add String(profileExpGet({_player})) to {_args::*}
    add formatNumber(profileStatisticsGet({_player}, "mineQuantity")) to {_args::*}
    add formatNumber(profileMoneyGet({_player})) to {_args::*}

    #Update Scoreboard
    set title of {_player}'s scoreboard to getDefine("SCOREBOARD_TITLE")
    set {_argumentIndex} to 1
    set {_contents::*} to getDefineList("SCOREBOARD_CONTENTS")
    loop {_contents::*}:
        set {_content} to loop-value
        while {_content} contains "${}":
            set {_content} to substitution({_content}, {_args::%{_argumentIndex}%})
            add 1 to {_argumentIndex}
        set line (loop-index parsed as integer) of {_player}'s scoreboard to {_content}
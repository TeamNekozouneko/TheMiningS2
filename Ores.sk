#==================================================
#| #Ores.sk
#| 鉱石の実装・管理モジュール
#|
#| Nekozouneko Group Developer
#==================================================
function breakHandle(player: player, block: block) :: boolean:
    set {_itemId} to getItemId({_player}'s tool)
    set {_blockType} to type of {_block}
    return false if {_itemId} is not set
    return false if getDefineList("BREAKABLE_ORES::%{_itemId}%") doesn't contain {_blockType}

    #ドロップの処理
    set {_dropItem} to getDefine("DROP_ITEMS::%{_blockType}%")
    drop {_dropItem} at {_block}

    #EXPの獲得処理
    profileExpIncrease({_player}, getDefine("EARN_EXP::%{_blockType}%"), "ORES_BREAK")

    #鉱石復活のキュー追加
    set {_unixtime} to floor(unix timestamp of now) #Unixtimeの整数部だけ扱う
    set {_roundedUnixtime} to {_unixtime} - mod({_unixtime}, 10) #一桁目の数字だけ引いて一桁目の情報を0にそろえる
    add {_roundedUnixtime} to {ores.revivePendingSpans::*}
    add (location of {_block}) to {ores.revivePendingLocations::*}
    add {_blockType} to {ores.revivePendingBlocks::*}

    return true

#鉱石復活の定期処理
function oreReviveAsyncHandler():
    run 0 ticks later repeating every 5 tick:
        loop getDefine("REVIVE_ORE_PER_INTERVAL") times:
            exit loop if size of {ores.revivePendingSpans::*} <= 0
            
            set {_revivePendingSpan} to the first element of {ores.revivePendingSpans::*}
            set {_revivePendingBlock} to the first element of {ores.revivePendingBlocks::*}
            set {_revivePendingLocation} to the first element of {ores.revivePendingLocations::*}

            set {_reviveTimePendingBlock} to getDefine("REVIVE_TIME::%{_revivePendingBlock}%")
            continue if {_reviveTimePendingBlock} is not set
            continue if getElapsedUnixtime({_revivePendingSpan}) < {_reviveTimePendingBlock}

            set block at {_revivePendingLocation} to {_revivePendingBlock}

            remove {_revivePendingSpan} from {ores.revivePendingSpans::*}
            remove {_revivePendingBlock} from {ores.revivePendingBlocks::*}
            remove {_revivePendingLocation} from {ores.revivePendingLocations::*}
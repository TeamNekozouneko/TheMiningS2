#==================================================
#| #Map.sk
#| マップの実装・管理モジュール
#|
#| Nekozouneko Group Developer
#==================================================

#以下ポータルの実装
function teleportDestination(p: player, destiName: string) :: boolean:
    set {_location} to getDefine("LOCATION::%{_destiName}%")
    if isDestinationAvailable({_location}) is false:
        return false
    force teleport {_p} to {_location}

    #クエスト VISIT_ANY_MINE
    questTriggerIncreaseHandle({_p}, "VISIT_ANY_MINE")
    
    return true

function isDestinationAvailable(desti: location) :: boolean:
    return false if {_desti} is not set
    return false if block at {_desti} is not set
    return false if block above {_desti} is not air
    return true

function portalStartTeleportation(p: player, portalName: string):
    set metadata value "isPortalTeleporting" of {_p} to true #Teleport Lock

    set {_waitingTick} to 20
    set {_tick} to 0
    set {_destiName} to getDefine("PORTAL_DESTINATION::%{_portalName}%")
    set {_pos1} to getDefine("PORTAL::%{_portalName}%::1")
    set {_pos2} to getDefine("PORTAL::%{_portalName}%::2")

    while {_tick} <= {_waitingTick}:
        send action bar getDefine("LANG::PORTAL_TELEPORTING") to {_p}

        if isWithinLocation({_pos1}, {_pos2}, {_p}'s location) is false:
            set {_cancelled} to true
            exit loop

        add 5 to {_tick}
        wait 5 tick

    if {_cancelled} is true:
        send action bar getDefine("LANG::PORTAL_CANCELLED") to {_p}
    else if teleportDestination({_p}, {_destiName}) is true:
        send action bar getDefine("LANG::PORTAL_TELEPORTED") to {_p}
    else:
        send action bar getDefine("LANG::ERROR_DESTINATION_NOT_AVAILABLE") to {_p}

    set metadata value "isPortalTeleporting" of {_p} to false #Teleport unLock

#ポータル範囲内かを定期検査するAsyncHandler
function portalConditionAsyncHandler():
    async run 0 ticks later repeating every second:
        set {_players::*} to all players where [metadata value "isPortalTeleporting" of input is false]
        loop getDefineList("PORTALS"):
            set {_pos1} to getDefine("PORTAL::%loop-value%::1")
            set {_pos2} to getDefine("PORTAL::%loop-value%::2")
            loop {_players::*} where [isWithinLocation({_pos1}, {_pos2}, input's location) is true]:
                portalStartTeleportation(loop-value-2, loop-value-1)

#以下ジャンプパッドの実装
function onJumpPadHandle(p: player, block: block):
    exit if block under {_block} is not gold block

    set {_speed} to 3
    set {_maxPitch} to 35
    set {_yaw} to {_p}'s yaw
    set {_pitch} to {_p}'s pitch

    set {_pitch} to abs({_pitch})
    set {_pitch} to {_maxPitch} if {_pitch} > {_maxPitch}

    set {_vector} to vector from yaw {_yaw} and pitch -1*{_pitch}
    push {_p} {_vector} at speed {_speed}


function isInRegion(p: player, region: string) :: boolean:
    set {_target} to {_p}'s location
    loop getDefineList("REGIONS"):
        set {_start} to getDefine("REGION::%loop-value%::1")
        set {_end} to getDefine("REGION::%loop-value%::2")
        return true if isWithinLocation({_start}, {_end}, {_target}) is true
    return false

function getRegion(p: player) :: string:
    set {_target} to {_p}'s location
    loop getDefineList("REGIONS"):
        set {_start} to getDefine("REGION::%loop-value%::1")
        set {_end} to getDefine("REGION::%loop-value%::2")
        return loop-value if isWithinLocation({_start}, {_end}, {_target}) is true
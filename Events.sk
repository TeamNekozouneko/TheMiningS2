#==================================================
#| #Events.sk
#| イベントの管理モジュール
#|
#| Nekozouneko Group Developer
#==================================================

on load:
    checkInstanceEnvironment()
    criticalErrorDetectionHandler()

    initDefine()
    initMap()
    oreReviveAsyncHandler()
    initNPCs()
    portalConditionAsyncHandler()
    enchantmentGuiAsyncHandler()
    enchantmentAutoSellPendingApplyHandler()
    smeltingSmeltOnGuiHandler()
    smeltingSmeltOnBackGroundHandler()
    questAssignHandler()
    questPeriodicCheckHandlerInit()
    questResetAssignHandler()

every second:
    loop all players:
        scoreboardUpdate(loop-player)

on first join:
    profileInit(player)
    onFirstJoinHandle(player)

on join:
    profileJoinInit(player)
    sendJoinMessage(player)

on break:
    cancel drops
    cancel event if onBreakOreHandle(player, event-block) is false

on place:
    cancel event

on citizen right click:
    onCitizenRightClickNPCHandle(player, event-npc)

on hunger meter change:
    cancel event

on damage:
    attacker or victim is a player
    cancel event

on step on pressure plate:
    cancel event
    onJumpPadHandle(player, event-block)

on inventory click:
    cancel event if onInventoryClickShopHandle(player, event-item) is true
    cancel event if onInventoryClickEnchantmentHandle(player, event-item) is true
    cancel event if onInventoryClickSmeltingHandle(player, event-item) is true
    cancel event if onInventoryClickQuestHandle(player, event-item) is true
    cancel event if onInventoryClickLevelupHandle(player, event-item) is true

on inventory close:
    onInventoryCloseShopHandle(player)
    onInventoryCloseEnchantmentHandle(player)
    onInventoryCloseSmeltingHandle(player)

on right click:
    cancel event if onRightClickEnchantmentHandle(player, event-block) is true
#==================================================
#| #enchantments → AutoSell.sk
#| エンチャント「AutoSell（自動売却）」の実装モジュール
#|
#| Nekozouneko Group Developer
#==================================================

Options:
    SELL_RATIO_PER_LEVEL: 10
    SELL_PROCESS_PENDING_APPLY_INTERVAL: 5

function enchantmentAutoSellHandle(p: player, items: objects, autoSellLevel: integer) :: objects: #return 売却できたアイテム
    exit if {_autoSellLevel} is not set
    exit if isInventoryFreeSpaceAvailable({_p}) is true
    set {_multiplier} to ({_autoSellLevel} * {@SELL_RATIO_PER_LEVEL}) / 100

    #売却処理
    loop {_items::*}:
        set {_itemId} to getItemId(loop-value)
        set {_price} to getDefine("SELL_PRICE::%{_itemId}%")
        set {_amount} to item amount of loop-value
        continue if {_itemId} is not set
        continue if {_price} is not set

        add {_price} * {_amount} to {_totalPrice}
        add loop-value to {_sellableItems::*}
    add {_totalPrice} * {_multiplier} to {autosell.pendingApply::%{_p}'s uuid%}
    return {_sellableItems::*}

function enchantmentAutoSellPendingApplyHandler():
    run 0 ticks later repeating every {@SELL_PROCESS_PENDING_APPLY_INTERVAL} second:
        loop all players:
            set {_money} to {autosell.pendingApply::%loop-player's uuid%}
            continue if {_money} is not set
            
            profileMoneyIncrease(loop-player, {_money}, "AUTO_SELL")
            clear {autosell.pendingApply::%loop-player's uuid%}
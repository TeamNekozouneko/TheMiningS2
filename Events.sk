#==================================================
#| #Events.sk
#| イベントの管理モジュール
#|
#| Nekozouneko Group Developer
#==================================================

on load:
    checkInstanceEnvironment()
    criticalErrorDetectionHandler()
    stabilizeSaveCurrentTps()

    initDefine()
    initMap()
    initTeam()
    oreReviveAsyncHandler()
    portalConditionAsyncHandler()
    enchantmentGuiAsyncHandler()
    enchantmentAutoSellPendingApplyHandler()
    smeltingSmeltOnGuiHandler()
    smeltingSmeltOnBackGroundHandler()
    questAssignHandler()
    questResetAssignHandler()
    profileTabListNameHandler()
    pickupPeriodicCheckHandlerInit()
    pickupPeriodicAnnounceHandlerInit()

    wait 1 ticks
    initNPCs()

every second:
    loop all players:
        scoreboardUpdate(loop-player)

on first join:
    profileInit(player)
    profileFirstJoinInit(player)

on join:
    profileJoinInit(player)
    profileSendJoinMessage(player)
    guideBossBarInit(player)
    mapTeleportSafeHandle(player)
    profileInventoryFixer(player)

    add player to {themining.team::nocol}

on quit:
    guideBossBarClear(player)

    remove player from {themining.team::nocol}

on break:
    cancel drops
    cancel event if onBreakOreHandle(player, event-block) is false

on place:
    cancel event

on craft:
    cancel event

on citizen right click:
    onCitizenRightClickNPCHandle(player, event-npc)

on hunger meter change:
    cancel event

on damage:
    attacker or victim is a player
    cancel event

on step on pressure plate:
    cancel event
    onJumpPadHandle(player, event-block)

on inventory click:
    cancel event if onInventoryClickShopHandle(player, event-item) is true
    cancel event if onInventoryClickEnchantmentHandle(player, event-item) is true
    cancel event if onInventoryClickSmeltingHandle(player, event-item) is true
    cancel event if onInventoryClickQuestHandle(player, event-item) is true
    cancel event if onInventoryClickLevelupHandle(player, event-item) is true
    cancel event if onInventoryClickGachaHandle(player) is true

on inventory close:
    onInventoryCloseShopHandle(player)
    onInventoryCloseEnchantmentHandle(player)
    onInventoryCloseSmeltingHandle(player)

on right click:
    exit if event-block is not set
    exit if event-block is ender chest
    cancel event
    onRightClickEnchantmentHandle(player, event-block)

on drop:
    cancel event
    #インベントリーに余裕がない時、cancel eventでアイテムが消えるため
    if player doesn't have enough space for event-item:
        pickupAddItem(player, event-item)
        send getDefine("LANG::PICKUP_STORED") to player
        playSound(getObjects(player), "Error")

on player interact:
    if event-blockaction is physical:
        if event-block is farmland:
            cancel event
#==================================================
#| #Ores.sk
#| 鉱石の実装・管理モジュール
#|
#| Nekozouneko Group Developer
#==================================================

Options:
    DO_NOT_REVIVE_AROUND_BLOCKS: 3

function onBreakOresSingleHandle(p: player, block: block) :: boolean:
    set {_item} to {_p}'s tool
    set {_itemId} to getItemId({_item})
    set {_blockType} to type of {_block}
    set {_blockLocation} to location of {_block}
    return false if {_itemId} is not set
    return false if getDefineList("BREAKABLE_ORES::%{_itemId}%") doesn't contain {_blockType}
    set {_dropItem} to getDefine("DROP_ITEMS::%{_blockType}%")

    set {_fortuneLevel} to getItemEnchantmentLevel({_item}, "fortune")
    set {_autoCollectLevel} to getItemEnchantmentLevel({_item}, "auto_collect")
    set {_autoSellLevel} to getItemEnchantmentLevel({_item}, "auto_sell")
    set {_rangeMiningLevel} to getItemEnchantmentLevel({_item}, "range_mining")
    #エンチャントの処理
    #| Fortune
    set {_dropItem} to enchantmentFortuneDropItemHandle({_dropItem}, {_fortuneLevel})
    #| AutoSell
    set {_isAutoSold} to true if enchantmentAutoSellSingleHandle({_p}, {_dropItem}, {_autoSellLevel}) is true
    #| AutoCollect
    if {_isAutoSold} is not true:
        set {_isDropCancelled} to true if enchantmentAutoCollectSingleHandle({_p}, {_dropItem}, {_autoCollectLevel}) is true
    #| RangeMining
    enchantmentRangeMiningHandle({_p}, {_block}, {_rangeMiningLevel})

    #ドロップの処理
    if {_isDropCancelled} is not true:
        if {_isAutoSold} is not true:
            drop {_dropItem} at {_blockLocation}

    #EXPの獲得処理
    profileExpIncrease({_p}, getDefine("EARN_EXP::%{_blockType}%"), "ORES_BREAK")

    #Statistics
    profileStatisticsIncrease({_p}, "mineQuantity", 1)

    #鉱石復活のキュー追加
    set {_unixtime} to floor(unix timestamp of now) #Unixtimeの整数部だけ扱う
    set {_roundedUnixtime} to {_unixtime} - mod({_unixtime}, 10) #一桁目の数字だけ引いて一桁目の情報を0にそろえる
    set {_queueId} to random integer between 1000000000 and 9999999999
    add {_queueId} to {ores.revivePendingQueues::*}
    set {ores.revivePendingSpans::%{_queueId}%} to {_roundedUnixtime}
    set {ores.revivePendingLocations::%{_queueId}%} to {_blockLocation}
    set {ores.revivePendingBlocks::%{_queueId}%} to {_blockType}

    return true

function onBreakOresMultipleHandle(p: player, blocks: objects) :: objects: #return キャンセルするブロックリスト
    set {_item} to {_p}'s tool
    set {_itemId} to getItemId({_item})
    exit if {_itemId} is not set

    #それぞれの情報をリスト型でまとめる
    loop {_blocks::*}:
        set {_blockType} to type of loop-value
        if getDefineList("BREAKABLE_ORES::%{_itemId}%") doesn't contain {_blockType}:
            add loop-value to {_allowedBlocks::*}
            continue

        add {_blockType} to {_blockTypes::*}
        add location of loop-value to {_blockLocations::*}
        add getDefine("DROP_ITEMS::%{_blockType}%") to {_dropItems::*}
        add getDefine("EARN_EXP::%{_blockType}%") to {_earnExp}
        add 1 to {_mineQuantity}

    set {_fortuneLevel} to getItemEnchantmentLevel({_item}, "fortune")
    set {_autoCollectLevel} to getItemEnchantmentLevel({_item}, "auto_collect")
    set {_autoSellLevel} to getItemEnchantmentLevel({_item}, "auto_sell")
    set {_rangeMiningLevel} to getItemEnchantmentLevel({_item}, "range_mining")
    #エンチャントの処理
    #| Fortune（1個のアイテムを入れて帰ってきた個数＝幸運の有効率）
    set {_fortuneAmount} to item amount of enchantmentFortuneDropItemHandle(stone, {_fortuneLevel})
    loop {_dropItems::*}:
        set item amount of loop-value to (item amount of loop-value) * {_fortuneAmount}
    #| AutoSell
    set {_sellableItems::*} to enchantmentAutoSellMultipleHandle({_p}, {_dropItems::*}, {_autoSellLevel})
    remove {_sellableItems::*} from {_dropItems::*}
    #| AutoCollect
    set {_isAutoCollected} to false
    set {_isAutoCollected} to true if enchantmentAutoCollectMultipleHandle({_p}, {_dropItems::*}, {_autoCollectLevel}) is true

    #ドロップの処理
    if {_isAutoCollected} is false:
        loop {_dropItems::*}:
            drop loop-value at (random element out of {_blockLocations::*})

    #EXPの獲得処理
    profileExpIncrease({_p}, {_earnExp}, "ORES_BREAK_MULTIPLE")

    #Statistics
    profileStatisticsIncrease({_p}, "mineQuantity", {_mineQuantity})

    #鉱石復活のキュー追加
    set {_unixtime} to floor(unix timestamp of now) #Unixtimeの整数部だけ扱う
    set {_roundedUnixtime} to {_unixtime} - mod({_unixtime}, 10) #一桁目の数字だけ引いて一桁目の情報を0にそろえる
    loop {_blockTypes::*}:
        set {_queueId} to random integer between 1000000000 and 9999999999
        add {_queueId} to {ores.revivePendingQueues::*}
        set {ores.revivePendingSpans::%{_queueId}%} to {_roundedUnixtime}
        set {ores.revivePendingLocations::%{_queueId}%} to {_blockLocations::%loop-index%}
        set {ores.revivePendingBlocks::%{_queueId}%} to {_blockTypes::%loop-index%}

    return {_allowedBlocks::*}

#鉱石復活の定期処理
function oreReviveAsyncHandler():
    run 0 ticks later repeating every 5 tick:
        copy {ores.revivePendingQueues::*} to {_queues::*} #元のリスト変数を動かしてはならぬ、データが不整合になるど

        loop getDefine("REVIVE_ORE_PER_INTERVAL") times:
            exit loop if size of {_queues::*} <= 0

            set {_revivePendingQueue} to random element out of (the first getDefine("REVIVE_ORE_PER_INTERVAL") elements of {_queues::*})
            set {_revivePendingSpan} to {ores.revivePendingSpans::%{_revivePendingQueue}%}
            set {_revivePendingBlock} to {ores.revivePendingBlocks::%{_revivePendingQueue}%}
            set {_revivePendingLocation} to {ores.revivePendingLocations::%{_revivePendingQueue}%}

            set {_reviveTimePendingBlock} to getDefine("REVIVE_TIME::%{_revivePendingBlock}%")
            continue if {_reviveTimePendingBlock} is not set
            continue if getElapsedUnixtime({_revivePendingSpan}) < {_reviveTimePendingBlock}
            continue if size of all players in radius {@DO_NOT_REVIVE_AROUND_BLOCKS} around {_revivePendingLocation} >= 1

            set block at {_revivePendingLocation} to {_revivePendingBlock}

            #キューから削除
            clear {ores.revivePendingSpans::%{_revivePendingQueue}%}
            clear {ores.revivePendingBlocks::%{_revivePendingQueue}%}
            clear {ores.revivePendingLocations::%{_revivePendingQueue}%}
            remove {_revivePendingQueue} from {ores.revivePendingQueues::*}
            
            remove {_revivePendingQueue} from {_queues::*}
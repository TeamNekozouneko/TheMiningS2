#==================================================
#| #NPCs.sk
#| NPCの実装・管理モジュール
#|
#| Nekozouneko Group Developer
#==================================================

function initNPCs():
    delete all npcs
    loop getDefineList("NPCS"):
        spawn a getDefine("NPC::%loop-value%::TYPE") citizen named getDefine("NPC::%loop-value%::NAME") at getDefine("NPC::%loop-value%::LOCATION")
        set {_npc} to last spawned npc
        set {_skin} to skin from url getDistributionURL(concat(getDefine("NPC::%loop-value%::SKIN"), ".png"))
        change skin of {_npc} to {_skin}
        wait 8 second #API先の429（Too many requests）対策用

function onCitizenRightClickNPCHandle(player: player, npc: npc):
    if compareName({_npc}, getDefine("NPC::LEVELUP::NAME")) is true:
        exit if talkHandle({_player}, "LEVELUP") is true
        levelupGuiHandle({_player})
    else if compareName({_npc}, getDefine("NPC::SHOP::NAME")) is true:
        exit if talkHandle({_player}, "SHOP") is true
        shopGuiHandle({_player})
    else if compareName({_npc}, getDefine("NPC::SMELTING::NAME")) is true:
        exit if talkHandle({_player}, "SMELTING") is true
        smeltingGuiHandle({_player})
    else if compareName({_npc}, getDefine("NPC::QUEST_MASTER::NAME")) is true:
        exit if talkHandle({_player}, "QUEST_MASTER") is true
        questGuiHandle({_player})
    else if compareName({_npc}, getDefine("NPC::YEAHN::NAME")) is true:
        talkHandle({_player}, "YEAHN")
    else if compareName({_npc}, getDefine("NPC::GACHA_T::NAME")) is true:
        gachaGuiHandle({_player}, "0")

local function compareName(npc: npc, name: string) :: boolean:
    return true if uncoloured mini message from (citizen name of {_npc}) is uncoloured {_name}
    return false